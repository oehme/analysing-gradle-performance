dependencies {
  compile project(':my-lib')
  testCompile project(path: ':my-lib', configuration: 'tests')

  def springVersion = '1.5.4.RELEASE'
  compile "org.springframework.boot:spring-boot-starter-web:$springVersion"
  compile "org.springframework.boot:spring-boot-starter-jetty:$springVersion"
  compile "org.springframework.boot:spring-boot-starter-actuator:$springVersion"
}

task uberJar(type:Jar) {
  inputs.files configurations.runtime
  from sourceSets.main.output
  from { configurations.runtime.collect { it.directory ? it : zipTree(it) } }
  classifier "uber-jar"
}

artifacts {
  archives uberJar
}

task projectStats() {
  def statsFile = new File(buildDir, 'stats.txt')
  inputs.files sourceSets.main.java
  outputs.file statsFile
  doLast {
    statsFile.parentFile.mkdirs()
    statsFile.text = "Source files: ${sourceSets.main.java.size()}"
  }
}
assemble.dependsOn(projectStats)
